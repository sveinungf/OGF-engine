CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

SET(NAME OGF_Engine)
PROJECT(${NAME})

SET(EXECUTABLE_OUTPUT_PATH "${PROJECT_BINARY_DIR}/bin")
SET(LIBRARY_OUTPUT_PATH "${PROJECT_BINARY_DIR}/lib")
SET(OGF_RESOURCE_DIR "\"${CMAKE_SOURCE_DIR}/resources\"")

CONFIGURE_FILE(
	"${CMAKE_SOURCE_DIR}/OGFConfig.h.in"
	"${PROJECT_BINARY_DIR}/include/OGFConfig.h"
)

IF(MSVC)
	SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)

	FOREACH(flag
		CMAKE_CXX_FLAGS CMAKE_CXX_FLAGS_DEBUG CMAKE_CXX_FLAGS_RELEASE
		CMAKE_CXX_FLAGS_MINSIZEREL CMAKE_CXX_FLAGS_RELWITHDEBINFO)
		IF(${flag} MATCHES "/MD")
			string(REGEX REPLACE "/MD" "/MT" ${flag} "${${flag}}")
		ENDIF()
	ENDFOREACH()
	
	FOREACH(flag
		CMAKE_C_FLAGS CMAKE_C_FLAGS_DEBUG CMAKE_C_FLAGS_RELEASE
		CMAKE_C_FLAGS_MINSIZEREL CMAKE_C_FLAGS_RELWITHDEBINFO)
		IF(${flag} MATCHES "/MD")
			string(REGEX REPLACE "/MD" "/MT" ${flag} "${${flag}}")
		ENDIF()
	ENDFOREACH()
ENDIF()

INCLUDE_DIRECTORIES(
	${CMAKE_SOURCE_DIR}/depends/freeglut-2.8.1/include
	${CMAKE_SOURCE_DIR}/depends/glew-1.10.0/include
	${CMAKE_SOURCE_DIR}/depends/glm-0.9.4.6/include
	${CMAKE_SOURCE_DIR}/depends/soil-20080707/src
	${CMAKE_SOURCE_DIR}/depends/assimp--3.0.1270-sdk/include
	${CMAKE_SOURCE_DIR}/include
	${PROJECT_BINARY_DIR}/include	#Files generated by CMake
)

SET(GLEW_NAME glew)
SET(GLUT_NAME freeglut_static)
SET(SOIL_NAME soil)
SET(ASSIMP_NAME assimp)

ADD_DEFINITIONS(
	-DGLEW_STATIC
	-DFREEGLUT_STATIC
)

if(MSVC)
	ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS)
ENDIF()

ADD_SUBDIRECTORY(depends/glew-1.10.0)
ADD_SUBDIRECTORY(depends/freeglut-2.8.1)

IF(MSVC)
	#Suppress warnings in AssImp
	ADD_DEFINITIONS(-D_SCL_SECURE_NO_WARNINGS)
	ADD_SUBDIRECTORY(depends/assimp--3.0.1270-sdk)
	REMOVE_DEFINITIONS(-D_SCL_SECURE_NO_WARNINGS)
	
	#Suppress warnings in SOIL
	SET(SOIL_WARNINGS
		"/wd4018"	#Signed/unsigned mismatch
		"/wd4244"	#Conversion from 'double' to 'float' or similar
	)
	ADD_DEFINITIONS(${SOIL_WARNINGS})
	ADD_SUBDIRECTORY(depends/soil-20080707)
	REMOVE_DEFINITIONS(${SOIL_WARNINGS})
ELSE()
	ADD_SUBDIRECTORY(depends/assimp--3.0.1270-sdk)
	ADD_SUBDIRECTORY(depends/soil-20080707)
ENDIF()

IF(CMAKE_COMPILER_IS_GNUCXX)
	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
ENDIF()

ADD_SUBDIRECTORY(src)

SET(DEPENDENCIES Dependencies)
SET_TARGET_PROPERTIES(${GLEW_NAME} PROPERTIES FOLDER ${DEPENDENCIES})
SET_TARGET_PROPERTIES(${GLUT_NAME} PROPERTIES FOLDER ${DEPENDENCIES})
SET_TARGET_PROPERTIES(${SOIL_NAME} PROPERTIES FOLDER ${DEPENDENCIES})
SET_TARGET_PROPERTIES(${ASSIMP_NAME} PROPERTIES FOLDER ${DEPENDENCIES}/AssImp)
SET_TARGET_PROPERTIES(uninstall PROPERTIES FOLDER ${DEPENDENCIES}/AssImp)
SET_TARGET_PROPERTIES(zlib PROPERTIES FOLDER ${DEPENDENCIES}/AssImp)
