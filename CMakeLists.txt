CMAKE_MINIMUM_REQUIRED(VERSION 2.8)

SET(NAME OGF_Engine)
PROJECT(${NAME})

SET(EXECUTABLE_OUTPUT_PATH "${PROJECT_BINARY_DIR}/bin")
SET(LIBRARY_OUTPUT_PATH "${PROJECT_BINARY_DIR}/lib")
SET(OGF_RESOURCE_DIR "\"${CMAKE_SOURCE_DIR}/resources\"")

CONFIGURE_FILE(
	"${CMAKE_SOURCE_DIR}/OGFConfig.h.in"
	"${PROJECT_BINARY_DIR}/include/OGFConfig.h"
)

INCLUDE_DIRECTORIES(
	${CMAKE_SOURCE_DIR}/depends/freeglut-2.8.1/include
	${CMAKE_SOURCE_DIR}/depends/glew-1.10.0/include
	${CMAKE_SOURCE_DIR}/depends/glm-0.9.4.6/include
	${CMAKE_SOURCE_DIR}/depends/soil-20080707/src
	${CMAKE_SOURCE_DIR}/depends/assimp--3.0.1270-sdk/include
	${CMAKE_SOURCE_DIR}/include
	${PROJECT_BINARY_DIR}/include	#Files generated by CMake
)

SET(GLEW_NAME glew)
SET(GLUT_NAME freeglut_static)
SET(SOIL_NAME soil)
SET(ASSIMP_NAME assimp)

ADD_DEFINITIONS(
	-DGLEW_STATIC
	-DFREEGLUT_STATIC
)

ADD_SUBDIRECTORY(depends/glew-1.10.0)
ADD_SUBDIRECTORY(depends/freeglut-2.8.1)
ADD_SUBDIRECTORY(depends/assimp--3.0.1270-sdk)
ADD_SUBDIRECTORY(depends/soil-20080707)
ADD_SUBDIRECTORY(src)

#Suppress warnings in third party libraries
IF(MSVC)
	SET_PROPERTY(TARGET
		${GLUT_NAME}
		${ASSIMP_NAME}
		APPEND PROPERTY COMPILE_DEFINITIONS _CRT_SECURE_NO_WARNINGS
	)
	
	SET_PROPERTY(TARGET
		${ASSIMP_NAME}
		APPEND PROPERTY COMPILE_DEFINITIONS _SCL_SECURE_NO_WARNINGS
	)
	
	SET_PROPERTY(TARGET
		${SOIL_NAME}
		APPEND_STRING PROPERTY COMPILE_FLAGS " /W1"
	)
ENDIF()

IF(MSVC)
	#Set warning level 4 for OGF source code
	SET_PROPERTY(TARGET
		${NAME}
		APPEND_STRING PROPERTY COMPILE_FLAGS " /W4"
	)
ELSEIF(CMAKE_COMPILER_IS_GNUCXX)
	#Use C++11 and enable all warnings for OGF source code
	SET_PROPERTY(TARGET
		${NAME}
		APPEND_STRING PROPERTY COMPILE_FLAGS " -std=c++11 -Wall -Wextra -pedantic"
	)
ENDIF()

#Project folder structure in Visual Studio
IF(MSVC)
	SET_PROPERTY(GLOBAL PROPERTY USE_FOLDERS ON)
	
	SET(DEPENDENCIES Dependencies)
	SET_TARGET_PROPERTIES(${GLEW_NAME} PROPERTIES FOLDER ${DEPENDENCIES})
	SET_TARGET_PROPERTIES(${GLUT_NAME} PROPERTIES FOLDER ${DEPENDENCIES})
	SET_TARGET_PROPERTIES(${SOIL_NAME} PROPERTIES FOLDER ${DEPENDENCIES})
	SET_TARGET_PROPERTIES(${ASSIMP_NAME} PROPERTIES FOLDER ${DEPENDENCIES}/AssImp)
	SET_TARGET_PROPERTIES(uninstall PROPERTIES FOLDER ${DEPENDENCIES}/AssImp)
	SET_TARGET_PROPERTIES(zlib PROPERTIES FOLDER ${DEPENDENCIES}/AssImp)
ENDIF()
